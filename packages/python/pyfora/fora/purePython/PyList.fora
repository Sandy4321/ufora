/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
class {
    member @m;
    
    //required because the AttributeError clause in the PyObjectBase mixin masks the
    //generated version of this attribute.
    ...(`Member, `@m) { @m };

    operator[]
    (PyInt(ix)) {
        self[ix.@m]
        }
    (ix) {
        try {
            @m[ix]
            }
        catch (e) {
            throw Exception((e, @m, ix))
            }
        }
    (*args) {
        PyList(@m[*args])
        };

    append: 
        throw InvalidPyforaOperation(
            "Appending to a list is a mutating operation, which pyfora doesn't support."
            );

    index: fun(value) {
        for ix in sequence(size(@m)) {
            if (@m[ix] == value)
                return PyInt(ix)
            }
        throw Exception("value %s is not in list".format(value))
        };

    __mul__: fun(PyInt(other)) {
        let toReturn = [];
        for i in sequence(other.@m) {
            toReturn = toReturn + @m
        }
        PyList(toReturn)
        };

    __eq__: fun(PyList(other)) {
        PyBool(@m == other.@m)
        }
        (_) {
        PyBool(false)
        };

    __ne__: fun(PyList(other)) {
        PyBool(@m != other.@m)
        }
        (_) {
        PyBool(true)
        };

    __gt__: fun(PyList(other)) {
        if(size(other.@m) == size(@m)) {
            return PyBool(false)
            }
        else if(size(@m) == 0) {
            return PyBool(false)
            }
        else return PyBool(true)
        }
        (PyString(other)) {
        PyBool(false)
        }
        (_) {
        PyBool(true)
        };

    __lt__: fun(PyList(other)) {
        if(size(other.@m) == size(@m)) {
            return PyBool(false)
            }
        else if(size(@m) == 0) {
            return PyBool(true)
            }
        else return PyBool(false)
        }
        (PyString(other)) {
        PyBool(true)
        }
        (_) {
        PyBool(false)
        };

    __len__: fun() {
        PyInt(Int64(size(@m)))
        };

    __str__: fun() {
        PyString(@m)
        };

    __pyfora_concat__: fun(other) {
        PyList(@m :: other)
        };

    operator match(PyList _) { (self,) };

    operator+(PyList(other)) {
        PyList(@m + other.@m)
        };

    operator iterator() {
        for val in @m 
            yield val
        };

    operator in (elt) {
        PyBool(elt in @m)
        };

    mixin PyObjectBase;
    };

