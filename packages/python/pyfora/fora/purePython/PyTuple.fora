/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
class {
    member @m;
    
    //required because the AttributeError clause in the PyObjectBase mixin masks the
    //generated version of this attribute.
    ...(`Member, `@m) { @m };

    ...(`RawGetItemByInt, arg) {
        @m[arg]
        };

    operator[]
    (PyInt(ix)) {
        self[ix.@m]
        }
    ({Int64} ix) {
        if (ix < -size(@m) or ix >= size(@m))
            throw IndexError(PyString("tuple index out of range"))
        return @m[ix]
        }
    (*args) {
        PyTuple(@m[*args])
        };

    index: fun(value) {
        for ix in sequence(size(@m)) {
            if (Bool(@m[ix] == value))
                return purePython.PyInt(ix)
            }
        throw ValueError("value %s is not in tuple".format(value))
        };

    __len__: fun() {
        size(@m)
        };

    __str__: fun() {
        PyString(@m)
        };

    operator match(PyTuple _) { (self,) };

    operator+(PyTuple(other)) {
        PyTuple(@m + other.@m)
        };

    operator iterator() {
        for val in @m
            yield val
        };

    operator in (elt) {
        PyBool(elt in @m)
        };

    mixin PyObjectBase;
    };

