/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
class {
    member @m;
    
    ...(`Member, `@m) { @m };

    __str__: fun() {
        PyString(String(@m))
        };

    convert(Bool _) { @m is not 0 };

    __nonzero__: fun() { PyBool(@m is not 0) };

    operator match(PyInt _) { (self,) };

    operator%(PyInt(other)) {
        PyInt(@m % other.@m)
        };

    operator==(PyInt(other)) {
        PyBool(@m == other.@m)
        };

    operator!=(PyInt(other)) {
        PyBool(@m != other.@m)
        };

    //Arithmetic operators
    __add__: fun(PyInt(other)) {
        PyInt(@m + other.@m)
        }
        (PyFloat(other)) {
        PyFloat(@m + other.@m)
        };

    __sub__: fun(PyInt(other)) {
        PyInt(@m - other.@m)
        }
        (PyFloat(other)) {
        PyFloat(@m - other.@m)
        };

    __mul__: fun(PyInt(other)) {
        PyInt(@m * other.@m)
        }
        (PyFloat(other)) {
        PyFloat(@m * other.@m)
        }
        (PyString(other)) {
        PyString(other.@m * @m)
        }
        (PyList(other)) {
        let toReturn = [];
        for i in sequence(@m) {
            toReturn = toReturn + other.@m
        }
        PyList(toReturn)
        };

    __div__: fun(PyInt(other)) {
        if(other.@m == 0) {
            throw ZeroDivisionError()
        }
        PyInt(Int64(math.floor(@m / Float64(other.@m))))
        }
        (PyFloat(other)) {
        if(other.@m == 0) {
            throw ZeroDivisionError()
        }
        PyFloat(@m / other.@m)
        };


    __pow__: fun(PyInt(other)) {
        PyFloat(@m ** other.@m)
        }
        (PyFloat(other)) {
        if(@m < 0 and (other.@m != math.floor(other.@m))) 
            {
            throw ValueError("Negative number cannot be raised to a fractional power")
            }
        PyFloat(@m ** other.@m)
        };

    __xor__: fun(PyInt(other)) {
        PyInt(@m ^ other.@m)
    };

    //Comparison operators
    __lt__: fun(PyInt(other)) {
        PyBool(@m < other.@m)
        }
        (PyNone(other)) {
        PyBool(false)
        }
        (PyFloat(other)) {
        PyBool(@m < other.@m)
        }
        (PyString(other)) {
        PyBool(true)
        }
        (PyList(other)) {
        PyBool(true)
        };

    __abs__: fun() {
        if(@m >= 0) {
        return PyInt(@m)
        } else {
        return PyInt(@m * -1)
        }
    };

    __gt__: fun(PyInt(other)) {
        PyBool(@m > other.@m)
        }
        (PyNone(other)) {
        PyBool(true)
        }
        (PyFloat(other)) {
        PyBool(@m > other.@m)
        }
        (PyString(other)) {
        PyBool(false)
        }
        (PyList(other)) {
        PyBool(false)
        };

    //Equality operators
    __eq__: fun(PyInt(other)) {
        PyBool(@m == other.@m)
        }
        (PyNone(other)) {
        PyBool(false)
        }
        (PyFloat(other)) {
        PyBool(@m == other.@m)
        }
        (PyString(other)) {
        PyBool(false)
        }
        (_) {
        PyBool(false)
        };


    __ne__: fun(PyInt(other)) {
        PyBool(@m != other.@m)
        } 
        (PyNone(other)) {
        PyBool(true)
        }
        (PyFloat(other)) {
        PyBool(@m != other.@m)
        }
        (PyString(other)) {
        PyBool(true)
        }
        (_) {
        PyBool(true)
        };

    mixin PyObjectBase;
    };

