/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
/****************
PyGeneratorBase

This instance is a mixin that provides useful functionality for implementations of generators.
****************/

object {
   __str__: fun() { return PyString("<generator object>") };

   map: fun(elementMapper) {
      let MappingGenerator = class {
         member @subgenerator;
         member @mapper;

         canSplit: fun() { return @subgenerator.canSplit(); };

         split: fun() { 
            match (@subgenerator.split()) with 
               (PyTuple(...) t) { PyTuple((MappingGenerator(t[0], @mapper), MappingGenerator(t[1], @mapper))) }
               (PyNone(...) res) { res }
               (...) { throw TypeError(PyString("Generator 'split' operations should return None or a pair of Generators")); }
               ; 
            };

         operator iterator() {
            for x in @subgenerator
               yield @mapper(x)
            };

         map: fun(elementMapper) {
            MappingGenerator(@subgenerator, fun(x) { elementMapper(@mapper) })
            };

         mixin PyGeneratorBase;
         }

      return MappingGenerator(self, elementMapper)
      };

   split: fun() { return PyNone(nothing); };

   canSplit: fun() { return PyBool(false); };

   __pyfora_generator__: fun() { self };

   mixin PyObjectBase;
   };