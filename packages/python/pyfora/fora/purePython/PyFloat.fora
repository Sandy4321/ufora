/***************************************************************************
   Copyright 2015 Ufora Inc.

   Licensed under the Apache License, Version 2.0 (the "License");
   you may not use this file except in compliance with the License.
   You may obtain a copy of the License at

       http://www.apache.org/licenses/LICENSE-2.0

   Unless required by applicable law or agreed to in writing, software
   distributed under the License is distributed on an "AS IS" BASIS,
   WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
   See the License for the specific language governing permissions and
   limitations under the License.
****************************************************************************/
class {
    member @m;
    
    ...(`Member, `@m) { @m };

    __str__: fun() {
        PyString(String(@m))
        };

    operator match(PyFloat _) { (self,) };

    operator%(PyFloat(other)) {
        PyFloat(@m % other.@m)
        };

    operator<(PyFloat(other)) {
        PyBool(@m < other.@m)
        };

    operator<=(PyFloat(other)) {
        PyBool(@m <= other.@m)
        };

    operator>(PyFloat(other)) {
        PyBool(@m > other.@m)
        };

    operator>=(PyFloat(other)) {
        PyBool(@m >= other.@m)
        };

    //Arithmetic operators
    __add__: fun(PyFloat(other)) {
        PyFloat(@m + other.@m)
        }
        (PyInt(other)) {
        PyFloat(@m + other.@m)
        };

    __sub__: fun(PyFloat(other)) {
        PyFloat(@m - other.@m)
        }
        (PyInt(other)) {
        PyFloat(@m - other.@m)
        };

    __mul__: fun(PyFloat(other)) {
        PyFloat(@m * other.@m)
        }
        (PyInt(other)) {
        PyFloat(@m * other.@m)
        };

    __div__: fun(PyInt(other)) {
        if(other.@m == 0) {
            throw "Zero Division Error"
        }
        PyFloat(@m / other.@m)
        }
        (PyFloat(other)) {
        if(other.@m == 0.0) {
            throw "Zero Division Error"
        }
        PyFloat(@m / other.@m)
        };

    __pow__: fun(PyFloat(other)) {
        if(@m < 0 and (other.@m != math.floor(other.@m))) 
            {
            throw "Negative number cannot be raised to a fractional power"
            }
        PyFloat(@m ** other.@m)
        }
        (PyInt(other)) {
        PyFloat(@m ** other.@m)
        };

    //Comparison operators
    __lt__: fun(PyFloat(other)) {
        PyBool(@m < other.@m)
        }
        (PyInt(other)) {
        PyBool(@m < other.@m)
        }
        (PyNone(other)) {
        PyBool(false)
        }
        (PyString(other)) {
        PyBool(true)
        }
        (PyList(other)) {
        PyBool(true)
        };

    __gt__: fun(PyFloat(other)) {
        PyBool(@m > other.@m)
        }
        (PyInt(other)) {
        PyBool(@m > other.@m)
        }
        (PyNone(other)) {
        PyBool(true)
        }
        (PyString(other)) {
        PyBool(false)
        }
        (PyList(other)) {
        PyBool(false)
        };

    //Equality operators
    __eq__: fun(PyFloat(other)) {
        PyBool(@m == other.@m)
        }
        (PyInt(other)) {
        PyBool(@m == other.@m)
        }
        (_) {
        PyBool(false)
        };

    __ne__: fun(PyFloat(other)) {
        PyBool(@m != other.@m)
        }
        (PyInt(other)) {
        PyBool(@m != other.@m)
        }
        (_) {
        PyBool(true)
        };

    
    mixin PyObjectBase;
    };

